--TEST--
DESSA 003
--ARGS--
-S
--CODE--
# Figure 8 (swap problem) from "Translating Out of Static Single Assignment Form" by Sreedhar
{
	uintptr_t c_1 = 0;
	bool c_2 = 0;
	bool c_3 = 1;
	int32_t c_4 = 1;
	int32_t c_12 = 12;
	l_1 = START(l_13);
	int32_t x0 = PARAM(l_1, "x", 0);
	int32_t y1 = PARAM(l_1, "y", 1);
	int32_t cond = PARAM(l_1, "z", 2);
	int32_t x1 = DIV(x0, c_12);
	l_3 = END(l_1);
	l_4 = LOOP_BEGIN(l_3, l_9);
	int32_t x2 = PHI(l_4, x1, x3);
	int32_t y2 = PHI(l_4, y1, y3);
	int32_t z = COPY(x2);
	int32_t x3 = COPY(y2);
	int32_t y3 = COPY(z);
	l_7 = IF(l_4, cond);
	l_8 = IF_TRUE(l_7);
	l_9 = LOOP_END(l_8, l_4);
	l_10 = IF_FALSE(l_7);
	l_11 = LOOP_EXIT(l_10, l_4);
	l_12 = BEGIN(l_11);
	l_13 = RETURN(l_12, z);
}
--EXPECT--
test:
	movl %edx, %r8d
	movl %edi, %eax
	movl $0xc, %ecx
	cltd
	idivl %ecx
	movl %eax, %ecx
.L1:
	testl %r8d, %r8d
	je .L2
	movl %esi, %eax
	movl %ecx, %esi
	movl %eax, %ecx
	jmp .L1
.L2:
	movl %ecx, %eax
	retq
