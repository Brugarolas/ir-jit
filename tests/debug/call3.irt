--TEST--
CALL with parallel argument passing
--TARGET--
x86_64
--ARGS--
-S
--CODE--
{
	uintptr_t c_6 = "hello %d!\n";
	l_1 = START(l_4);
	int32_t p_1 = PARAM(l_1, "p1", 1);
	int32_t p_2 = PARAM(l_1, "p2", 2);
	int32_t p_3 = PARAM(l_1, "p3", 3);
	int32_t p_4 = PARAM(l_1, "p4", 4);
	uintptr_t fp = PARAM(l_1, "fp", 5);
	uintptr_t f, l_11 = LOAD(l_1, fp);	
	int32_t d_2, l_2 = CALL/4(l_11, f, c_6, p_3, p_4, p_2);
	int32_t d_3 = ADD(d_2, p_1);
	l_4 = RETURN(l_2, d_3);
}
--EXPECT--
test:
	subq $8, %rsp
	movq %rbx, (%rsp)
	movl %edi, %ebx
	movl %ecx, %eax
	movl %esi, %ecx
	movl %edx, %esi
	movl %eax, %edx
	leaq .L1(%rip), %rdi
	callq *(%r8)
	addl %ebx, %eax
	movq (%rsp), %rbx
	addq $8, %rsp
	retq
.rodata
	.db 0x90
.L1:
	.db 0x68, 0x65, 0x6c, 0x6c, 0x6f, 0x20, 0x25, 0x64, 0x21, 0x0a, 0x00
